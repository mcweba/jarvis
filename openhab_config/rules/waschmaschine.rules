val Number MODE_OFF = 0
val Number MODE_STANDBY = 1
val Number MODE_ACTIVE = 2
val Number MODE_FINISHED = 3

var Boolean Wasseralarm_Notified = false

rule "Washingmachine Status"
when
    Item MyStrom_Waschmaschine_Power changed
then
    if (MyStrom_Waschmaschine_Power.state < 0.2) { 
           postUpdate(MyStrom_Waschmaschine_State, MODE_OFF) 
           logInfo("Waschmaschine", "MODE_OFF")
    } else if (MyStrom_Waschmaschine_Power.state > 5) {
            if (MyStrom_Waschmaschine_State.state != MODE_ACTIVE) {           
                postUpdate(MyStrom_Waschmaschine_State, MODE_ACTIVE)        
                logInfo("Waschmaschine", "MODE_ACTIVE")
            }
    } else if (MyStrom_Waschmaschine_Power.state < 3) {
           if (MyStrom_Waschmaschine_State.state == MODE_OFF) {
              postUpdate(MyStrom_Waschmaschine_State, MODE_STANDBY)
              logInfo("Waschmaschine", "MODE_STANDBY")
    } else if (MyStrom_Waschmaschine_State.state == MODE_ACTIVE) {
              postUpdate(MyStrom_Waschmaschine_State, MODE_FINISHED)
              logInfo("Waschmaschine", "MODE_FINISHED")
           }
       }
end

rule "Washingmachine finished notification"
when
    Item MyStrom_Waschmaschine_State changed
then
    if (MyStrom_Waschmaschine_State.state == MODE_FINISHED) {
        sendTelegram("MyMaHome_Marc", "Die Wäsche ist fertig.")
        sendTelegram("MyMaHome_Myriam", "Die Wäsche ist fertig.")
    }
end

rule "Wasseralarm Heizung Notifikation"
when
    Item HZ_Aqara_Wasseralarm changed
then
    if (HZ_Aqara_Wasseralarm.state == 1 && Wasseralarm_Notified == false) {
        logWarn("Heizung", "Wasseralarm in Heizung detected!")
        sendTelegram("MyMaHome_Marc", "Achtung! Wasseralarm in Heizung!")
        //sendTelegram("MyMaHome_Myriam", "Achtung! Wasseralarm in Heizung!")
        Wasseralarm_Notified = true
    } else if (HZ_Aqara_Wasseralarm.state == 0){
        Wasseralarm_Notified = false
    }
end